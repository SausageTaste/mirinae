#version 450

#include "../utils/konst.glsl"

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout (binding = 0, rgba32f) uniform writeonly image2D out_hkt_dxdy;
layout (binding = 1, rgba32f) uniform writeonly image2D out_hkt_dz;
layout (binding = 2, rgba32f) uniform writeonly image2D out_hkt_ddxddz;
layout (binding = 3, rgba32f) uniform readonly image2D in_hk;


layout (push_constant) uniform U_OceanTildeHKtPushConst {
    float time;
} u_pc;

const int N = 256;
const int L = 100;


struct Complex {
    float re;
    float im;
};

Complex complex_mul(Complex a, Complex b) {
    return Complex(a.re * b.re - a.im * b.im, a.re * b.im + a.im * b.re);
}

Complex complex_add(Complex a, Complex b) {
    return Complex(a.re + b.re, a.im + b.im);
}

Complex complex_conj(Complex a) {
    return Complex(a.re, -a.im);
}


void main() {
    const ivec2 global_idx = ivec2(gl_GlobalInvocationID.xy);

    const vec2 x = ivec2(gl_GlobalInvocationID.xy) - float(N) / 2.0;
    const vec2 k = x * 2.0 * PI / float(L);
    float mag = length(k);
    if (mag < 0.00001)
        mag = 0.00001;

    const float w = sqrt(GRAVITY_EARTH * mag);
    const vec4 h0k_texel = imageLoad(in_hk, global_idx);
    const Complex fourier_cmp = Complex(h0k_texel.x, h0k_texel.y);
    const Complex fourier_cmp_conj = Complex(h0k_texel.z, h0k_texel.w);

    const float cos_wt = cos(w * u_pc.time);
    const float sin_wt = sin(w * u_pc.time);

    Complex exp_iwt = Complex(cos_wt, sin_wt);
    Complex exp_iwt_inv = Complex(cos_wt, -sin_wt);

    Complex hkt_dy = complex_add(
        complex_mul(fourier_cmp, exp_iwt),
        complex_mul(fourier_cmp_conj, exp_iwt_inv)
    );
    Complex hkt_dx = complex_mul(Complex(0, -k.x / mag), hkt_dy);
    Complex hkt_dz = complex_mul(Complex(0, -k.y / mag), hkt_dy);
    imageStore(out_hkt_dxdy, global_idx, vec4(hkt_dx.re, hkt_dx.im, hkt_dy.re, hkt_dy.im));
    imageStore(out_hkt_dz, global_idx, vec4(hkt_dz.re, hkt_dz.im, 0, 1));

    Complex hkt_slopex = complex_mul(Complex(0, k.x), hkt_dy);
    Complex hkt_slopez = complex_mul(Complex(0, k.y), hkt_dy);
    imageStore(out_hkt_ddxddz, global_idx, vec4(hkt_slopex.re, hkt_slopex.im, hkt_slopez.re, hkt_slopez.im));
}
