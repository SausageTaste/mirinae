#version 450

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout (binding = 0, rgba32f) uniform writeonly image2D out_displacement[3];
layout (binding = 1, rgba8) uniform writeonly image2D out_normal[3];
layout (binding = 2, rgba32f) uniform readonly image2D in_hkt_dxdy[3];
layout (binding = 3, rgba32f) uniform readonly image2D in_hkt_dz[3];
layout (binding = 4, rgba32f) uniform readonly image2D in_hkt_ddxddz[3];

layout (push_constant) uniform U_OceanFinalizePushConst {
    vec2 jacobian_lambda;
    float foam_threshold;
    float foam_bias;
    float foam_add;
    int N;
    int idx;
} u_pc;


void main() {
    ivec2 global_idx = ivec2(gl_GlobalInvocationID.xy);
    int cascade = int(gl_GlobalInvocationID.z);

    float perms[2] = float[2](1.0, -1.0);
    int index = int(mod((int(global_idx.x) + int(global_idx.y)), 2));
    float perm = perms[index];
    const float norm_factor = perm / float(u_pc.N * u_pc.N);

    vec4 texel_dxdy = imageLoad(in_hkt_dxdy[cascade], global_idx) * norm_factor;
    vec4 texel_dz = imageLoad(in_hkt_dz[cascade], global_idx) * norm_factor;
    vec4 texel_ddxddz = imageLoad(in_hkt_ddxddz[cascade], global_idx) * norm_factor;

    vec3 displacement = vec3(texel_dxdy.xz, texel_dz.x);

    vec3 normal = vec3(texel_ddxddz.x, texel_ddxddz.z, 1);
    normal.y *= -1;
    normal = normalize(normal);
    normal = normal * 0.5 + 0.5;

    const vec2 lambda = u_pc.jacobian_lambda;
    float jacobian = (1.0 + lambda.x * texel_ddxddz.x) * (1.0 + lambda.y * texel_ddxddz.z);
    float biasedJacobian = max(0.0, -(jacobian - u_pc.foam_threshold));
    float foam = 0.0;
    if (biasedJacobian > u_pc.foam_threshold)
        foam += u_pc.foam_add * biasedJacobian;

    imageStore(out_displacement[cascade], global_idx, vec4(displacement.x, -displacement.y, displacement.z, foam));
    imageStore(out_normal[cascade], global_idx, vec4(normal, 1));
}
